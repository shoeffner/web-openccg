@@grammar :: OpenCCG

start
    = semspec $
    ;

semspec
    = term
    | nominal
    | variable
    | role
    ;

term
    = '(' @:'^'.{ nominal }* ')'
    | '(' @:semspec ')'
    ;

variable::Variable
    = name:variable_name roles:'^'.{ role }*
    | '(' ~ name:variable_name roles:'^'.{ role }* ')'
    ;

nominal::Nominal::Variable
    = '@' ~ name:variable_name '(' ~ roles:'^'.{ role }* ')'
    ;

role::Role
    = '<' type:atom '>' target:variable
    | '<' type:atom '>' target:atom
    | type:`entity` target:atom
    ;

variable_name::str
    = /[a-z]\d+(:[a-zA-Z\-]+)?/
    ;

atom::str
    = /[a-zA-Z\-\.]+/
    ;
