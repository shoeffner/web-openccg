@@grammar :: OpenCCG

start
    = semspec $
    ;

semspec
    = term
    | { nominal }+
    | { variable }+
    | { role }+
    ;

term
    = '(' @:'^'.{ nominal }* ')'
    | '(' @:semspec ')'
    ;

variable_string
    = name:variable_name ':' ~ type:variable_type
    | name:variable_name
    ;

variable::Variable
    = >variable_string roles:'^'.{ role }*
    | '(' >variable_string roles:'^'.{ role }* ')'
    ;

nominal::Nominal::Variable
    = '@' >variable_string '(' ~ roles:'^'.{ role }* ')'
    ;

role::Role
    = '<' type:atom '>' target:variable
    | '<' type:atom '>' target:atom
    | type:`entity` target:atom
    ;

variable_name::str
    = /[a-z]\d+/
    ;

variable_type::str
    = /[a-zA-Z\-]+/
    ;

atom::str
    = /[a-zA-Z\-\._0-9]+/
    ;
